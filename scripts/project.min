;Generates a new project page.
""        :ident
""        :title
""        :subtitle
""        :summary
false     :github
""        :release
false     :valid-id

(
  (str) expect first :ident
  ident "^[a-z0-9-]+$" match? :valid-regexp
  (. "contents") => "/" join ls :filelist
  filelist (filename "(.+)\..+$" search 1 get) map ident in? not :valid-file
  valid-regexp valid-file and
) ^validate


(valid-id not) (
  "ID" ask @ident
  ident validate @valid-id
  (valid-id not) ("ID must not be already used and it must contain only lowercase letters, numbers, or -" warn!) when
) while

"Title"     ask @title
"Subtitle"  ask @subtitle
"Summary"   ask @summary
"Version"   ask @release

"-----
id: $1
github: $1
home: /$1/
title: \"$2\"
subtitle: \"$3\"
summary: \"$4\"
content-type: project
active: true
version: $5
download: \"https://github.com/h3rald/$1/releases/download/\"
-----
" (ident title subtitle summary release) =% :metadata

("Create project?" confirm) 
(
  (ident ".md") => "" join :fn 
  (. "contents" fn) => "/" join :path
  metadata path fwrite
) when
